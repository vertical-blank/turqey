
buildscript {
  repositories {
    jcenter()
    maven{ 
      url 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
  }
  dependencies {
    classpath "us.hexcoder:gradle-twirl:1.0.0-SNAPSHOT"
    classpath "org.flywaydb:flyway-gradle-plugin:3.2.1"
  }
}

apply plugin: "twirl"
apply plugin: "scala"
apply plugin: "java"
apply plugin: "war"
apply plugin: "application"
apply plugin: 'org.flywaydb.flyway'

repositories {
  jcenter()
  mavenCentral()
  maven{ 
    url 'http://amateras.sourceforge.jp/mvn/'
  }
}

configurations {
  executableWar
}

dependencies {
  compile 'org.scala-lang:scala-library:2.11.6'
  compile 'org.scala-lang:scala-xml:2.11.0-M4'
  compile 'com.typesafe.play:twirl-api_2.11:1.0.2'
  compile group: 'com.typesafe.akka', name: 'akka-actor_2.11', version: '2.3.6'
  compile group: 'com.enragedginger', name: 'akka-quartz-scheduler_2.11', version: '1.4.0-akka-2.3.x'
  compile group: 'org.scalatra', name: 'scalatra_2.11', version: '2.3.0'
  compile group: 'com.typesafe.scala-logging', name: 'scala-logging-slf4j_2.11', version: '2.1.2'

  compile 'io.github.gitbucket:markedj:1.0.5'
  compile 'com.google.apis:google-api-services-oauth2:v1-rev99-1.20.0'
  compile 'com.google.apis:google-api-services-plus:v1-rev290-1.20.0'
  compile 'com.google.apis:google-api-services-plusDomains:v1-rev198-1.20.0'
  
  compile 'org.scalikejdbc:scalikejdbc_2.11:2.2.9'
  compile 'org.scalikejdbc:scalikejdbc-config_2.11:2.2.9'
  compile 'org.scalikejdbc:scalikejdbc-test_2.11:2.2.9'
  compile 'ch.qos.logback:logback-classic:1.1.3'
  compile 'org.flywaydb:flyway-core:3.2.1'

  compile 'com.h2database:h2:1.4.190'
  compile 'com.googlecode.java-diff-utils:diffutils:1.2.1'
  compile 'org.json4s:json4s-core_2.11:3.3.0'
  compile 'org.json4s:json4s-native_2.11:3.3.0'

  compile 'javax.mail:javax.mail-api:1.5.4'
  compile 'com.sun.mail:javax.mail:1.5.4'
  
  compile 'eu.medsea.mimeutil:mime-util:2.1.3'
  compile 'com.google.guava:guava:19.0'

  compile 'org.eclipse.jetty:jetty-webapp:8.1.18.v20150929'
  compile 'org.eclipse.jetty:jetty-server:8.1.18.v20150929'

  executableWar 'org.eclipse.jetty:jetty-webapp:8.1.18.v20150929'
  executableWar 'org.eclipse.jetty:jetty-server:8.1.18.v20150929'

  testCompile group: 'org.scalatra', name: 'scalatra-specs2_2.11', version: '2.3.0'
  testCompile group: 'org.scalatra', name: 'scalatra-scalatest_2.11', version: '2.3.1'
}

sourceSets.all {
  compileClasspath += configurations.executableWar
}

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

defaultTasks 'compileScala'

mainClassName = 'JettyLauncher'

compileScala {
    scalaCompileOptions.fork = true
    scalaCompileOptions.forkOptions.jvmArgs = ['-XX:MaxPermSize=512m']
    scalaCompileOptions.additionalParameters = [
        "-feature",
        "-language:reflectiveCalls", // used for config structural typing
        "-language:postfixOps"
    ]
}

twirl {
  imports = [
    "java.lang._",
    "java.util._",
    "scala.collection.JavaConversions._",
    "scala.collection.JavaConverters._",
    "play.twirl.api._",
    "turqey.servlet.ServletContextHolder._",
    "turqey.servlet.SessionHolder"
  ]
}

task h2(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  standardInput = System.in

  logging.captureStandardOutput LogLevel.QUIET

  main = 'org.h2.tools.Server'
  args '-web','-webAllowOthers'
  //args '-url', 'jdbc:h2:./db/default;MVCC=true', '-user', 'sa', '-password', 'sa'
}

flyway {
  driver = 'org.h2.Driver'
  url = 'jdbc:h2:file:./db/turqey'
  user = 'sa'
  password = 'sa'
  schemas = ['PUBLIC']
}

war {
  from {
    configurations.executableWar.collect {
      return it.isDirectory() ? it : zipTree(it)
    }
  }
  from "$buildDir/classes/main"

  exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'

  manifest.attributes('Main-Class': 'JettyLauncher')
}


