
buildscript {
  repositories {
    jcenter()
    maven{ 
      url 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenLocal()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
    maven {
      url "http://yohei224.github.io/"
    }
  }
  dependencies {
    classpath "us.hexcoder:gradle-twirl:1.0.0-SNAPSHOT"
    classpath "org.flywaydb:flyway-gradle-plugin:3.2.1"
    classpath 'com.github.yohei224:gradle-scalate-plugin:0.0.3'
    classpath "com.github.maiflai:gradle-scalatest:0.11"
  }
}

apply plugin: "twirl"
apply plugin: "scala"
apply plugin: "scalate"
apply plugin: "java"
apply plugin: "war"
apply plugin: "application"
apply plugin: 'org.flywaydb.flyway'
apply plugin: "com.github.maiflai.scalatest"

repositories {
  jcenter()
  mavenCentral()
  maven{ 
    url 'http://amateras.sourceforge.jp/mvn/'
  }
  maven {
    url "http://yohei224.github.io/"
  }
}

configurations {
  executableWar
}

dependencies {
  compile 'com.github.yohei224:gristle:0.0.1'

  compile 'org.scala-lang:scala-library:2.11.7'
  compile 'org.scala-lang:scala-reflect:2.11.7'
  compile 'org.scala-lang:scala-compiler:2.11.7'
  compile 'com.typesafe.play:twirl-api_2.11:1.0.2'
  compile 'org.scalatra:scalatra_2.11:2.4.0'
  compile 'org.scalatra:scalatra-scalate_2.11:2.4.0'
  
  compile 'ch.qos.logback:logback-classic:1.1.3'
  compile 'com.typesafe.scala-logging:scala-logging_2.11:3.1.0'
  
  compile 'com.typesafe.akka:akka-actor_2.11:2.3.6'
  compile 'com.enragedginger:akka-quartz-scheduler_2.11:1.4.0-akka-2.3.x'

  compile 'io.github.gitbucket:markedj:1.0.5'
  //compile 'com.google.apis:google-api-services-oauth2:v1-rev99-1.20.0'
  //compile 'com.google.apis:google-api-services-plus:v1-rev290-1.20.0'
  //compile 'com.google.apis:google-api-services-plusDomains:v1-rev198-1.20.0'
  
  compile 'org.scalikejdbc:scalikejdbc_2.11:2.3.5'
  compile 'org.scalikejdbc:scalikejdbc-config_2.11:2.3.5'
  compile 'org.scalikejdbc:scalikejdbc-test_2.11:2.3.5'
  
  compile 'org.flywaydb:flyway-core:3.2.1'
  compile 'commons-pool:commons-pool:1.6'
  compile 'commons-dbcp:commons-dbcp:1.4'

  compile 'com.h2database:h2:1.4.190'
  compile 'com.googlecode.java-diff-utils:diffutils:1.2.1'
  compile 'org.json4s:json4s-core_2.11:3.3.0'
  compile 'org.json4s:json4s-native_2.11:3.3.0'

  compile 'javax.mail:javax.mail-api:1.5.4'
  compile 'com.sun.mail:javax.mail:1.5.4'
  
  compile 'com.google.guava:guava:19.0'

  compile 'org.apache.tika:tika-core:1.12'

  providedCompile 'org.eclipse.jetty:jetty-server:9.2.15.v20160210'
  providedCompile 'org.eclipse.jetty:jetty-webapp:9.2.15.v20160210'
  providedCompile 'org.eclipse.jetty:jetty-servlet:9.2.15.v20160210'
  providedCompile 'org.eclipse.jetty:jetty-util:9.2.15.v20160210'
  providedCompile 'org.eclipse.jetty:jetty-security:9.2.15.v20160210'
  providedCompile 'org.eclipse.jetty:jetty-http:9.2.15.v20160210'
  providedCompile 'org.eclipse.jetty:jetty-io:9.2.15.v20160210'
  providedCompile 'org.eclipse.jetty:jetty-xml:9.2.15.v20160210'
  providedCompile 'javax.servlet:javax.servlet-api:3.0.1'
  
  executableWar 'org.eclipse.jetty:jetty-server:9.2.15.v20160210'
  executableWar 'org.eclipse.jetty:jetty-webapp:9.2.15.v20160210'
  executableWar 'org.eclipse.jetty:jetty-servlet:9.2.15.v20160210'
  executableWar 'org.eclipse.jetty:jetty-util:9.2.15.v20160210'
  executableWar 'org.eclipse.jetty:jetty-security:9.2.15.v20160210'
  executableWar 'org.eclipse.jetty:jetty-http:9.2.15.v20160210'
  executableWar 'org.eclipse.jetty:jetty-io:9.2.15.v20160210'
  executableWar 'org.eclipse.jetty:jetty-xml:9.2.15.v20160210'

  //testCompile group: 'org.scalatra', name: 'scalatra-specs2_2.11', version: '2.3.0'
  testCompile group: 'org.scalatra', name: 'scalatra-scalatest_2.11', version: '2.3.1'
  testCompile 'org.scalatest:scalatest_2.11:2+'
  testRuntime 'org.pegdown:pegdown:1.1.0'
}

sourceSets.all {
  //compileClasspath += configurations.executableWar
}

targetCompatibility = '1.7'
sourceCompatibility = '1.7'

defaultTasks 'compileScala'

mainClassName = 'JettyLauncher'

compileScala {
    scalaCompileOptions.fork = true
    scalaCompileOptions.forkOptions.jvmArgs = ['-XX:MaxPermSize=512m']
    scalaCompileOptions.additionalParameters = [
        "-feature",
        "-language:reflectiveCalls", // used for config structural typing
        "-language:postfixOps"
    ]
}

twirl {
  imports = [
    "java.lang._",
    "java.util._",
    "scala.collection.JavaConversions._",
    "scala.collection.JavaConverters._",
    "play.twirl.api._",
    "turqey.servlet.ServletContextHolder._",
    "turqey.servlet.SessionHolder"
  ]
}

task h2(type: JavaExec) {
  classpath = sourceSets.main.runtimeClasspath
  standardInput = System.in

  logging.captureStandardOutput LogLevel.QUIET

  main = 'org.h2.tools.Server'
  args '-web','-webAllowOthers'
  //args '-url', 'jdbc:h2:./db/default;MVCC=true', '-user', 'sa', '-password', 'sa'
}

flyway {
  driver = 'org.h2.Driver'
  url = 'jdbc:h2:file:./db/turqey'
  user = 'sa'
  password = 'sa'
  schemas = ['PUBLIC']
}

war {
  from {
    configurations.executableWar.collect {
      return it.isDirectory() ? it : zipTree(it)
    }
  }
  
  from "$buildDir/classes/main"

  exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA','WEB-INF/lib/jetty-*.jar'

  manifest.attributes('Main-Class': 'JettyLauncher')
}

run {
  if (['run', 'test'].contains(project.gradle.startParameter.taskNames.pop())){
    println "remove precompiled templates..."
    new File("$buildDir/classes/main/templates/").deleteDir()
    println 'disable precompile task'
    tasks.scalatePreCompile.enabled = false
  }
}


